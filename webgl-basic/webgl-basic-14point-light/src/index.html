<!--
Ref: https://webglfundamentals.org/
-->
<div class="wrapper">
  <canvas id="c">Your browser does not seem to support HTML5 canvas.</canvas>
</div>
<script type="x-shader/x-vertex">
  #version 100
  
  #ifdef GL_ES
  precision mediump float;
  #endif
  // 데이터 저장할 속성 (사용자정의)
  attribute vec4 a_position;
  attribute vec4 a_color;
  attribute vec3 a_normal; // 법선벡터!!
  
  // 데이터 저장할 유니폼(사용자정의)
  uniform mat4 u_matrix; // 행렬
  uniform mat4 u_world; //환경 프로젝션 행렬
  uniform vec3 u_pointLight; // 포인트 라이트 행렬!!
  uniform vec3 u_viewWorld; // 카메라 위치 행렬!!
    
  // 베링을 사용해서 프레그먼트로 동일한 동명 배링으로 전달
  varying vec4 v_color;
  varying vec3 v_normal; // 법선벡터!!
  varying vec3 v_surfaceToLight; // 표면에서 빛 방향 벡터!!
  varying vec3 v_surfaceToView; // 표면에서 카메라 방향!!
  
  void main() {
    // 행렬 적용하고 정점 셰이더에 위치
    gl_Position = u_matrix * a_position;
    // 베링으로 복사
    v_color = a_color;
    // 회전/스케일 적용한 법선벡터를 셰이더로 전달
    v_normal = mat3(u_world) * a_normal;
    // v_normal = (u_world * vec4(a_normal,0)).xyz;
    
    // 표면과 월드 벡터를 곱해서 표면 벡터 구함!!
    vec3 surfaceWorldPos = (u_world * a_position).xyz;
    // 표면 법선과 포인트 라이트 차이로 표변->빛 벡터 구함!!
    v_surfaceToLight = u_pointLight - surfaceWorldPos;
    // 표면 법선과 카메라 방향 벡터!!
    v_surfaceToView = u_viewWorld - surfaceWorldPos;
  }
</script>
<script type="x-shader/x-fragment">
  #version 100
  
  // 기본 정밀도 설정
  #ifdef GL_ES
  precision mediump float; // 중간 정밀도
  #endif
  
  // 데이터 저장할 유니폼
  // uniform vec3 u_reverseLightDirection;
  uniform vec4 u_color;
  uniform float u_shininess;
  uniform vec3 u_lightColor; // color of linght
  uniform vec3 u_specularColor; // color of specular
    
  // 전달받는 베링
  varying vec4 v_color;
  varying vec3 v_normal; // 전달받은 법선벡터
  varying vec3 v_surfaceToLight; // 전달받은 표면->빛 벡터
  varying vec3 v_surfaceToView; // 전달받은 표면->카메라 벡터!!

  void main() {
    // 단위벡터로 만듬!!
    vec3 normal = normalize(v_normal);
    vec3 surfaceToLight = normalize(v_surfaceToLight);
    vec3 surfaceToView = normalize(v_surfaceToView);
    // 표면->빛과 표면->카메라 사이에 중간 벡터 구함!!
    vec3 halfVector = normalize(surfaceToLight + surfaceToView);
    
    // 법선벡터와 표면->빛 벡터 내적으로 빛각도 획득!!
    float light = dot(normal, surfaceToLight);
    // !!
    // float specular = dot(normal, halfVector);
    float specular = 0.0;
    if (light > 0.0) { // 빛이 반사되는 부분!!
      // 중간 벡터와 법선벡터의 내적값에 shininess로 지수적 폴오프 시킴!!
      specular = pow(dot(normal, halfVector), u_shininess);
    }
    
    // 프레그먼트 셰이더의 색
    gl_FragColor = v_color;
    // 색상채널에 빛 방향 곱하기
    gl_FragColor.rgb *= light * u_lightColor;
    // specular 추가!!
    gl_FragColor.rgb += specular * u_specularColor;
  }
</script>